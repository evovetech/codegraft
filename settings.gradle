/*
 * Copyright 2018 evove.tech
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    gradle.ext.configDir = file('gradle/config').absoluteFile

    def root = "https://gist.githubusercontent.com/laynepenney/ae549913ce1f25ab98c19bf77d7e920d/raw"
    def version = "75f68c513e92161e4d6cbe16151f180a6ce0e648"
    apply from: "${root}/${version}/buildscript.gradle"
    ext.configDir = gradle.configDir

    repositories(gradle.buildscriptRepos)
    dependencies(gradle.buildscriptDependencies)
}

apply plugin: 'evovetech.settings'
apply from: "${configDir}/plugins.gradle"

def add(String parent, String child) {
    def name = "${parent}-${child}"
    def path = ":${name}"
    include(path)
    project(path).projectDir = file("${parent}/${child}")
}

def addAll(String parent, String... includes) {
    includes.each { include ->
        add(parent, include)
    }
}

def codegen(String... includes) {
    addAll('codegen', *includes)
}

def inject(String... includes) {
    addAll('inject', *includes)
}

def inject_extension(String... includes) {
    addAll('inject-extension', *includes)
}

codegen(
        'model',
        'lib-processor',
        'app-processor'
)

inject(
        'annotations',
        'core',
        'android',
        'runtime',
        'transform',
        'plugin',
)

inject_extension(
        'crashlytics',
        'okhttp3'
)

include(
        ':sourcerer:io',
        ':sourcerer:utils',
)

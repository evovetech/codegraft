/*
 * Copyright (C) 2018 evove.tech
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: 'maven-publish'

def pubName = ext.pubName
def pub = publishing.publications.create(pubName, MavenPublication)
def pompom = pom

def configureAndroid(configurations, pub, sourceSet) {
    pub.pom.withXml {
        final dependenciesNode = asNode().appendNode('dependencies')

        ext.addDependency = { Dependency dep, String scope ->
            if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified") {
                return // ignore invalid dependencies
            }

            final dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dep.group)
            dependencyNode.appendNode('artifactId', dep.name)
            dependencyNode.appendNode('version', dep.version)
            dependencyNode.appendNode('scope', scope)

            if (!dep.transitive) {
                // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                exclusionNode.appendNode('groupId', '*')
                exclusionNode.appendNode('artifactId', '*')
            } else if (!dep.properties.excludeRules.empty) {
                // Otherwise add specified exclude rules
                final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                dep.properties.excludeRules.each { ExcludeRule rule ->
                    exclusionNode.appendNode('groupId', rule.group ?: '*')
                    exclusionNode.appendNode('artifactId', rule.module ?: '*')
                }
            }
        }

        def compileDeps = configurations.api.getAllDependencies()
        compileDeps += configurations.getByName(sourceSet.apiConfigurationName).getAllDependencies()
        def runtimeDeps = configurations.implementation.getAllDependencies()
        runtimeDeps += configurations.getByName(sourceSet.implementationConfigurationName).getAllDependencies()
        runtimeDeps -= compileDeps

        // List all "api" dependencies (for new Gradle) as "compile" dependencies
        compileDeps.each { dep -> addDependency(dep, "compile") }
        // List all "implementation" dependencies (for new Gradle) as "runtime" dependencies
        runtimeDeps.each { dep -> addDependency(dep, "runtime") }
    }
}

afterEvaluate {
    def android = extensions.findByName("android")
    configure(pub) {
        if (android) {
            groupId pompom.groupId

            def sourceSet = android.sourceSets
                    .matching { it.name == 'release' }
                    .first()
            configureAndroid(configurations, pub, sourceSet)

            def variants
            if (android.hasProperty('libraryVariants')) {
                variants = android.libraryVariants
            } else {
                variants = android.applicationVariants
            }
            def variant = variants
                    .matching { it.name == 'release' }
                    .first()
            def output = variant.outputs.first()
            artifact(output.outputFile) {
                extension "aar"
                builtBy(variant.assemble)
            }
//            artifact("${buildDir}/outputs/aar/${project.name}-release.aar")
        } else {
            from components.java
        }
        if (project.tasks.findByName('sourcesJar')) {
            artifact(sourcesJar) {
                classifier "sources"
            }
        }
        if (project.tasks.findByName('javadocJar')) {
            artifact(javadocJar) {
                classifier "javadoc"
            }
        }
    }
    def pomArtifactId = ext.pomArtifactId()
    publishing.publications.all {
        project.configure(it) {
            artifactId = pomArtifactId
            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig
            }
        }
    }
}

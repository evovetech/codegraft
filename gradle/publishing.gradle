/*
 * Copyright 2018 evove.tech
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "${configDir}/pom.gradle"
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

def pubName = "${project.name}Default"
publishing.publications {
    create(pubName, MavenPublication) {
        from components.java
        artifactId = getPomArtifactId()
        pom.withXml {
            def root = asNode()
            root.children().last() + pomConfig
        }
    }
}

def bintrayOrgRepo = "${app.bintrayOrg}/${app.bintrayRepo}"
def pomGroupId = pom.groupId
def pomVersion = pom.version
artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            if (pomVersion.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }

            if (project.hasProperty('bintrayUser')
                    && project.hasProperty('bintrayKey')
            ) {
                username = project.bintrayUser
                password = project.bintrayKey
            }
        }
        defaults {
            publications(pubName)
            properties = [
                    'bintray.repo': bintrayOrgRepo,
                    'bintray.package': pomGroupId,
                    'bintray.version': pomVersion
            ]
        }
    }
}

bintray {
    publications = [pubName]
    pkg {
        repo = app.bintrayRepo
        name = getPomArtifactId()
        userOrg = app.bintrayOrg
        licenses = rootProject.ext.LICENSES
        labels = rootProject.ext.LABELS
        websiteUrl = pom.url
        issueTrackerUrl = pom.issueTrackerUrl
        vcsUrl = pom.scmUrl
        publicDownloadNumbers = true
        version {
            released = new Date()
            name = pomVersion
        }
    }
}


if (project.hasProperty('bintrayUser')
        && project.hasProperty('bintrayKey')
) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey

    repositories {
        maven {
            url "https://dl.bintray.com/evove-tech/maven"
            credentials {
                username bintray.user
                password bintray.key
            }
        }
    }
}

if (project.hasProperty('mavenSync')
        && project.hasProperty('sonatypeUsername')
        && project.hasProperty('sonatypePassword')
) {
    def sync = bintray.pkg.version.mavenCentralSync
    sync.sync = true
    sync.user = project.sonatypeUsername
    sync.password = project.sonatypePassword
}

def getPomArtifactId() {
    def artifactId = project.pom?.artifactId
    if (artifactId == null) {
        def n = project.name
        println("setting artifactId=${n}")
        artifactId = n
    } else {
        println("getting artifactId=${artifactId}")
    }
    return artifactId
}

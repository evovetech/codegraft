/*
 * Copyright 2018 evove.tech
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "${configDir}/pom.gradle"
apply plugin: 'maven-publish'

def pubName = "${project.name}Default"

def getPomArtifactId() {
    def artifactId = project.pom?.artifactId
    if (artifactId == null) {
        def n = project.name
        println("setting artifactId=${n}")
        artifactId = n
    } else {
        println("getting artifactId=${artifactId}")
    }
    return artifactId
}

publishing.publications {
    create(pubName, MavenPublication) {
        from components.java
        artifactId = getPomArtifactId()
        pom.withXml {
            def root = asNode()
            root.children().last() + pomConfig
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    publications = [getPomArtifactId()]
    pkg {
        repo = app.bintrayRepo
        name = project.name
        userOrg = app.bintrayOrg
        licenses = rootProject.ext.LICENSES
        labels = rootProject.ext.LABELS
        websiteUrl = pom.url
        issueTrackerUrl = pom.issueTrackerUrl
        vcsUrl = pom.scmUrl
    }
}

def trays = [
        'sourcerer',
        'sourcerer.extensions'
]

if (project.hasProperty('bintrayUser')
        && project.hasProperty('bintrayKey')
) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey

    repositories {
        trays.each { tray ->
            maven {
                url "https://evovetech.bintray.com/${tray}"
                credentials {
                    username bintray.user
                    password bintray.key
                }
            }
        }
    }
}

if (project.hasProperty('mavenSync')
        && project.hasProperty('sonatypeUsername')
        && project.hasProperty('sonatypePassword')
) {
    def sync = bintray.pkg.version.mavenCentralSync
    sync.sync = true
    sync.user = project.sonatypeUsername
    sync.password = project.sonatypePassword
}
